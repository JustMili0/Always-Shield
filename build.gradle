plugins {
    id 'dev.architectury.loom' version '1.13-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.gradleup.shadow' version '8.3.6' apply false
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
    minecraft_version = rootProject.minecraft_version
    neo_version_range = rootProject.neo_version_range
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base { archivesName = rootProject.display_name }

    repositories {
    }

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.officialMojangMappings()
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    def detectedPlatform = ''
    if (project.name.toLowerCase().contains('fabric') || project.path.toLowerCase().contains('fabric')) {
        detectedPlatform = 'Fabric'
    } else if (project.name.toLowerCase().contains('neoforge') || project.path.toLowerCase().contains('neoforge') || project.name.toLowerCase().contains('forge')) {
        detectedPlatform = 'NeoForge'
    } else {
        detectedPlatform = ''
    }

    if (detectedPlatform) {
        project.version = "${rootProject.mod_version}+mc${rootProject.minecraft_version}-${detectedPlatform}"
    } else {
        project.version = rootProject.mod_version
    }

    tasks.withType(Jar).configureEach { Jar jarTask ->
        jarTask.archiveBaseName.set(rootProject.display_name)
        jarTask.archiveVersion.set(project.version.toString())
    }

    tasks.matching { it.name == 'shadowJar' }.configureEach { task ->
        if (task.hasProperty('archiveBaseName')) {
            task.archiveBaseName.set(rootProject.display_name)
            task.archiveVersion.set(project.version.toString())
        }
    }
}

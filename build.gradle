plugins {
    id 'dev.architectury.loom' version '1.13-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id "io.github.pacifistmc.forgix" version "2.0.0-SNAPSHOT.5.1"
    id 'com.gradleup.shadow' version '8.3.6' apply false
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = "${rootProject.mod_version}+mc${rootProject.minecraft_version}" //for file building
    mod_version = rootProject.mod_version
    minecraft_version = rootProject.minecraft_version
    neo_version_range = rootProject.neo_version_range
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base {
        archivesName = rootProject.display_name
    }

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.officialMojangMappings()
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }
}

forgix {
    destinationDirectory = layout.buildDirectory.dir("libs")
    archiveClassifier = "FQFNF"

    fabric()
    forge()
    neoforge()
}